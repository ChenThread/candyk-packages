# Maintainer: Adrian Siekierka <kontakt@asie.pl>
pkgname=candyk-mipsel-elf-binutils
pkgver=2.34
pkgrel=1
epoch=
pkgdesc="GNU binary utilities"
arch=("i686" "x86_64")
url="http://www.gnu.org/software/binutils"
license=("GPL-3.0-or-later")
source=("http://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.xz")
depends=(candyk-runtime-musl)
makedepends=(candyk-runtime-musl-devel)
groups=(candyk-mipsel-elf-toolchain)

. "../candyk-runtime-musl/env.sh"

prepare() {
	mkdir -p "binutils-$pkgver-build"
}

build() {
	cd "binutils-$pkgver"-build
	../"binutils-$pkgver"/configure --prefix=/opt/candyk/arch/mipsel-elf --target=mipsel-elf \
		--disable-multilib \
		--disable-nls \
		--disable-shared \
		--enable-lto \
		--disable-werror \
		--with-float=soft
	make configure-host
	make LDFLAGS="$_runtimeldflags"
}

package() {
	cd "binutils-$pkgver"-build
	make DESTDIR="$pkgdir" install
	rm "$pkgdir"/opt/candyk/arch/mipsel-elf/share/info/dir
}
sha256sums=('f00b0e8803dc9bab1e2165bd568528135be734df3fabf8d0161828cd56028952')
