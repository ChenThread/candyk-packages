# Maintainer: Adrian Siekierka <kontakt@asie.pl>
pkgname=(candyk-mipsel-elf-gcc candyk-mipsel-elf-gcc-libs)
pkgver=9.3.0
pkgrel=1
_gmpver=6.1.2
_mpfrver=4.0.2
_mpcver=1.1.0
_islver=0.21
epoch=
pkgdesc="The GNU Compiler Collection"
arch=("i686" "x86_64")
depends=(candyk-runtime-musl candyk-mipsel-elf-binutils)
makedepends=(candyk-runtime-musl-devel)
groups=(candyk-mipsel-elf-toolchain)
url="https://gcc.gnu.org"
license=("GPL-3.0-or-later")
source=("http://ftp.gnu.org/gnu/gcc/gcc-$pkgver/gcc-$pkgver.tar.xz"
        "http://gmplib.org/download/gmp/gmp-$_gmpver.tar.xz"
        "http://www.mpfr.org/mpfr-$_mpfrver/mpfr-$_mpfrver.tar.xz"
        "http://ftp.gnu.org/gnu/mpc/mpc-$_mpcver.tar.gz"
	"http://isl.gforge.inria.fr/isl-$_islver.tar.bz2"
	"0003-gcc-poison-system-directories.patch")

. "../candyk-runtime-musl/env.sh"

prepare() {
	mkdir -p "gcc-$pkgver-build"
	cd "gcc-$pkgver"
	patch -p1 <../0003-gcc-poison-system-directories.patch

	# HACK: hijack RTEMS's libstdc++ crossconfig for our own purposes (which has the dynamic feature checks we want)
	sed -i "s/\*-rtems\*/*-unknown*/" libstdc++-v3/configure

	ln -s ../"gmp-$_gmpver" gmp
	ln -s ../"mpfr-$_mpfrver" mpfr
	ln -s ../"mpc-$_mpcver" mpc
	ln -s ../"isl-$_islver" isl
}

build() {
	cd "gcc-$pkgver"-build
	../"gcc-$pkgver"/configure --prefix=/opt/candyk/arch/mipsel-elf --target=mipsel-elf \
		--with-sysroot="/opt/candyk/arch/mipsel-elf" \
		--with-bugurl="http://candyk.asie.pl" \
		--enable-languages=c,c++,lto \
		--with-stage1-ldflags="$_runtimeldflags -static-libgcc -static-libstdc++" \
		--disable-bootstrap \
		--disable-gcov \
		--disable-multilib \
		--disable-nls \
		--disable-shared \
		--disable-werror \
		--enable-lto \
		--enable-poison-system-directories \
		--disable-libstdcxx-pch \
		--disable-libunwind-exceptions \
		--with-float=soft \
		--with-isl \
		--with-newlib

	make
}

package_candyk-mipsel-elf-gcc() {
	cd "gcc-$pkgver"-build
	make DESTDIR="$pkgdir" install-gcc install-libcc1
	rm "$pkgdir"/opt/candyk/arch/mipsel-elf/share/info/dir
}

package_candyk-mipsel-elf-gcc-libs() {
	pkgdesc="GCC-provided libraries"
	depends=("candyk-mipsel-elf-binutils" "candyk-mipsel-elf-gcc" "candyk-mipsel-elf-newlib")
	options=(!strip)

	cd "gcc-$pkgver"-build
	make DESTDIR="$pkgdir" install-target-libgcc install-target-libstdc++-v3
	rm "$pkgdir"/opt/candyk/arch/mipsel-elf/lib/gcc/mipsel-elf/$pkgver/include/unwind.h
}

sha256sums=(
	'71e197867611f6054aa1119b13a0c0abac12834765fe2d81f35ac57f84f742d1'
	'87b565e89a9a684fe4ebeeddb8399dce2599f9c9049854ca8c0dfbdea0e21912'
	'1d3be708604eae0e42d578ba93b390c2a145f17743a744d8f3f8c2ad5855a38a'
	'6985c538143c1208dcb1ac42cedad6ff52e267b47e5f970183a3e75125b43c2e'
	'd18ca11f8ad1a39ab6d03d3dcb3365ab416720fcb65b42d69f34f51bf0a0e859'
	'bcdf0c9837557be871e885d25cebb61e3b4856e8c3dae735fe66398ba1d1b0c4'
)
