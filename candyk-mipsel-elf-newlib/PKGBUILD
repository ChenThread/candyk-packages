# Maintainer: Adrian Siekierka <kontakt@asie.pl>
pkgname=candyk-mipsel-elf-newlib
pkgver=3.3.0
pkgrel=1
epoch=
pkgdesc="C library for embedded systems"
arch=("i686" "x86_64")
depends=("candyk-mipsel-elf-binutils" "candyk-mipsel-elf-gcc")
url="https://sourceware.org/newlib"
license=("GPL-3.0-or-later")
source=("ftp://sourceware.org/pub/newlib/newlib-$pkgver.tar.gz")
options=(!strip)
groups=(candyk-mipsel-elf-toolchain)

prepare() {
	mkdir -p "newlib-$pkgver-build"
}

build() {
	cd "newlib-$pkgver"-build
	../"newlib-$pkgver"/configure --prefix=/opt/candyk/arch/mipsel-elf --target=mipsel-elf \
		--enable-lto \
		--with-float=soft \
		--disable-nls \
		--disable-multilib \
		--enable-newlib-nano-malloc \
		--enable-newlib-reent-small \
		--disable-newlib-reent-check-verify

	make
}

package() {
	cd "newlib-$pkgver"-build
	make DESTDIR="$pkgdir" install
}

sha256sums=('58dd9e3eaedf519360d92d84205c3deef0b3fc286685d1c562e245914ef72c66')
