# Maintainer: Adrian Siekierka <kontakt@asie.pl>
pkgname=candyk-mipsel-elf-newlib
pkgver=4.1.0
pkgrel=1
epoch=
pkgdesc="C library for embedded systems"
arch=("i686" "x86_64")
depends=("candyk-mipsel-elf-binutils" "candyk-mipsel-elf-gcc")
url="https://sourceware.org/newlib"
license=("GPL-3.0-or-later")
source=("ftp://sourceware.org/pub/newlib/newlib-$pkgver.tar.gz")
options=(!strip)
groups=(candyk-mipsel-elf-toolchain)

prepare() {
	mkdir -p "newlib-$pkgver-build"
}

build() {
	cd "newlib-$pkgver"-build
	../"newlib-$pkgver"/configure --prefix=/opt/candyk/arch/mipsel-elf --target=mipsel-elf \
		--enable-lto \
		--with-float=soft \
		--disable-nls \
		--disable-multilib \
		--enable-newlib-reent-small

	make
}

package() {
	cd "newlib-$pkgver"-build
	make DESTDIR="$pkgdir" install
}

sha256sums=('f296e372f51324224d387cc116dc37a6bd397198756746f93a2b02e9a5d40154')
