# Maintainer: Adrian Siekierka <kontakt@asie.pl>
pkgname=(candyk-psx-libs candyk-psx-tools candyk-psx-examples)
pkgver=20200111
_commit=6a2de5d244c5c36adb3cada9b114e0b2bb119d25
pkgrel=2
epoch=
pkgdesc="candyk PSX toolchain"
arch=("i686" "x86_64")
url="http://candyk.asie.pl"
license=("zlib")
groups=(candyk-psx)
source=("candyk-psx::git+https://github.com/ChenThread/candyk-psx.git#commit=$_commit")
makedepends=(candyk-runtime-musl-devel)

. "../candyk-runtime-musl/env.sh"

prepare() {
	cd "candyk-psx"
	rm bin/dummy
	rm lib/dummy
	sed -i -e "s/NATIVE_LDFLAGS =/NATIVE_LDFLAGS = ${_runtimeldflags//\//\\/}/" Makefile
}

build() {
	cd "candyk-psx"
	make
}

package_candyk-psx-libs() {
	depends=(candyk-mipsel-elf-gcc-libs candyk-mipsel-elf-newlib)
	options=(!strip)

	cd "candyk-psx"
	mkdir -p "$pkgdir"/opt/candyk/platform/psx
	cp -Rv include "$pkgdir"/opt/candyk/platform/psx/
	cp -Rv lib "$pkgdir"/opt/candyk/platform/psx/
}

package_candyk-psx-tools() {
	depends=(candyk-runtime-musl candyk-runtime-ffmpeg)

	cd "candyk-psx"
	mkdir -p "$pkgdir"/opt/candyk/platform/psx
	cp -Rv bin "$pkgdir"/opt/candyk/platform/psx/
}

package_candyk-psx-examples() {
	depends=(candyk-psx-libs)

	cd "candyk-psx"
	mkdir -p "$pkgdir"/opt/candyk/platform/psx
	cp -Rv examples "$pkgdir"/opt/candyk/platform/psx/
}

md5sums=('SKIP')
