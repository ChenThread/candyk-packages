# Maintainer: Adrian Siekierka <kontakt@asie.pl>
pkgname=(mipsel-elf-gcc mipsel-elf-gcc-libs)
pkgver=9.2.0
pkgrel=1
_gmpver=6.1.2
_mpfrver=4.0.2
_mpcver=1.1.0
_islver=0.21
epoch=
pkgdesc="The GNU Compiler Collection"
arch=("i686" "x86_64")
depends=("mipsel-elf-binutils")
url="https://gcc.gnu.org"
license=("GPL-3.0-or-later")
source=("http://ftp.gnu.org/gnu/gcc/gcc-$pkgver/gcc-$pkgver.tar.xz"
        "http://gmplib.org/download/gmp/gmp-$_gmpver.tar.xz"
        "http://www.mpfr.org/mpfr-$_mpfrver/mpfr-$_mpfrver.tar.xz"
        "http://ftp.gnu.org/gnu/mpc/mpc-$_mpcver.tar.gz"
	"http://isl.gforge.inria.fr/isl-$_islver.tar.bz2"
        "gcc-9.2.0-fix-91474.patch")

prepare() {
	mkdir -p "gcc-$pkgver-build"
	cd "gcc-$pkgver"
	patch -p2 <../gcc-9.2.0-fix-91474.patch

	# HACK: hijack RTEMS's libstdc++ crossconfig for our own purposes (which has the dynamic feature checks we want)
	sed -i "s/\*-rtems\*/*-unknown*/" libstdc++-v3/configure

	ln -s ../"gmp-$_gmpver" gmp
	ln -s ../"mpfr-$_mpfrver" mpfr
	ln -s ../"mpc-$_mpcver" mpc
	ln -s ../"isl-$_islver" isl
}

build() {
	cd "gcc-$pkgver"-build
	LDFLAGS="$LDFLAGS -static" ../"gcc-$pkgver"/configure --prefix=/opt/candyk/arch/mipsel-elf --target=mipsel-elf \
		--with-sysroot="/opt/candyk/arch/mipsel-elf" \
		--enable-languages=c,c++,lto \
		--disable-gcov \
		--disable-multilib \
		--disable-shared \
		--disable-werror \
		--disable-nls \
		--enable-lto \
		--enable-plugins \
		--disable-libstdcxx-pch \
		--disable-libunwind-exceptions \
		--with-float=soft \
		--with-isl \
		--with-newlib

	make
}

package_mipsel-elf-gcc() {
	cd "gcc-$pkgver"-build
	make DESTDIR="$pkgdir" install-gcc install-libcc1
	rm "$pkgdir"/opt/candyk/arch/mipsel-elf/share/info/dir
}

package_mipsel-elf-gcc-libs() {
	pkgdesc="GCC-provided libraries"
	depends=("mipsel-elf-binutils" "mipsel-elf-gcc" "mipsel-elf-newlib")
	options=(!strip)

	cd "gcc-$pkgver"-build
	make DESTDIR="$pkgdir" install-target-libgcc install-target-libstdc++-v3
	rm "$pkgdir"/opt/candyk/arch/mipsel-elf/lib/gcc/mipsel-elf/$pkgver/include/unwind.h
}

md5sums=('3818ad8600447f05349098232c2ddc78'
         'f58fa8001d60c4c77595fbbb62b63c1d'
         '320fbc4463d4c8cb1e566929d8adc4f8'
         '4125404e41e482ec68282a2e687f6c73'
         'e2be862b79d74234e468981bcae2a964'
         '9ea5dbe2e44366e14eb00c895ab2caff')
